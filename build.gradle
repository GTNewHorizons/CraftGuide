Properties ver = new Properties()
File versionFile = new File('version.txt')

// build is to distinguish bugfix releases, as the fourth number in the mod version. Resets when any of the others increase.
ver.build = 0

// with the default values set above, now try reading the actual values
ver.load(versionFile.newDataInputStream())

buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'https://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
        maven {
          url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath 'gradle.plugin.com.matthewprenger:CurseGradle:1.0.9'
    }
}

apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.matthewprenger.cursegradle'

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

String minecraft_versions = '1.9.4'
version = '1.7.1.' + ver.build
group = 'uristqwerty'
archivesBaseName = 'CraftGuide'

minecraft {
    version = '1.9.4-12.17.0.1976'
    runDir = 'run'
    mappings = 'stable_26'

    replace '@MOD_VERSION@', project.version
    replaceIn 'CraftGuide_FML.java'
}

repositories {
    maven {
        name = 'Player\'s Repo'
        url = 'http://maven.ic2.player.to/'
    }
    ivy {
        name 'BuildCraft'
        artifactPattern 'https://www.mod-buildcraft.com/releases/BuildCraft/[revision]/[module]-[revision](-[classifier]).[ext]'
    }
    maven {
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'k-4u'
        url 'https://modmaven.k-4u.nl'
    }
}

dependencies {
    compile 'de.ellpeck.actuallyadditions:ActuallyAdditions:1.10.2-r75:api'
    compile 'net.industrial-craft:industrialcraft-2:2.5.8-ex19:dev'
    compile files('manual-deps/Calculator-1.10.2-3.2.1.jar')
    compile files('manual-deps/SonarCore-1.10.2-3.2.6.jar')
    deobfCompile name: 'buildcraft', version: '7.2.8', classifier: 'api'
    deobfCompile 'slimeknights:TConstruct:1.9.4-2.3.3.jenkins263'
    deobfCompile 'slimeknights.mantle:Mantle:1.9.4-0.10.2.jenkins149'
}

task themeZip(type: Zip) {
    from 'themes'
    archiveName 'CraftGuideResources.zip'
    destinationDir sourceSets.main.output.resourcesDir
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property 'version', project.version
    inputs.property 'mcversion', project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

processResources.dependsOn themeZip

jar {
    classifier = 'forge'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'dev'
}

task apiJar(type: Jar) {
    from (sourceSets.main.output) {
        include 'uristqwerty/CraftGuide/api/**'
    }
    from (sourceSets.main.allJava) {
        include 'uristqwerty/CraftGuide/api/**'
    }
    classifier = 'api'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task publish(type: Exec) {
    commandLine 'cmd', '/c', 'publish.bat', project.version, minecraft_versions
}

curseforge {
    apiKey = project.curseForgeApiKey
    project {
        id = '75557'
        releaseType = 'release' //alpha|beta|release
        changelog = file('changes.txt')

        addGameVersion minecraft_versions
        mainArtifact jar
        addArtifact deobfJar
        addArtifact apiJar
        addArtifact sourcesJar
    }
}

publish.dependsOn build
//curseforge.dependsOn publish

artifacts {
    archives deobfJar
    archives apiJar
    archives sourcesJar
}

build.doLast {
    ver.build = '' + ((ver.build as int) + 1)
    ver.store(versionFile.newWriter(), null)
}

